import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com.agilesolutions'
version = '1.0.27'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

}

ext {
    commit = commit()
    branchName = branchName()
    // see https://spring.io/projects/spring-cloud
    set('springCloudVersion', "2022.0.3")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'com.azure.spring:spring-cloud-azure-feature-management-web:5.13.0'
    implementation 'com.azure.spring:spring-cloud-azure-appconfiguration-config-web:5.13.0'
    implementation 'com.azure.spring:spring-cloud-azure-appconfiguration-config:5.13.0'
    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'org.projectlombok:lombok:1.18.34'
    implementation 'com.microsoft.azure:applicationinsights-core:3.5.3'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

String commit() {
    return 'git rev-parse HEAD'.execute().text.trim()
}

String branchName() {
    return hasProperty('branch.name') ? property('branch.name') : 'git branch --show-current'.execute().text.trim()
}

task copyAppInsightsAgent(type: Copy) {
    from "${rootDir}/libs/applicationinsights-agent.jar"
    into "${buildDir}/libs"
}

task generateHelmChart(type: Copy) {
    inputs.dir('helm/demo')
    inputs.file('build.gradle')
    from('helm') {
        include 'demo/**'
        filter(ReplaceTokens, tokens: [appVersion: version])
    }
    into('build')

}

bootJar {

    archiveFileName = './application.jar'

    manifest{
        attributes(
                'Built-by'          : System.properties['user.name'],
                'Build-Timestamp'   : java.time.Instant.now(),
                'Build-Revision'    : commit,
                'Build-Branch'      : branchName,
                'Created-by'        : "Gradle ${gradle.gradleVersion}",
                'Build-JDK'         : "${System.properties['java.version']} ${System.properties['java.vendor']} ${System.properties['java.vm.version']}",
                'Build-OS'          : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )

    }

    dependsOn copyAppInsightsAgent

}

task release(type: GradleBuild) {
    tasks = ['clean', 'build']
}
