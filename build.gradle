import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com.agilesolutions'
version = '1.0.27'
sourceCompatibility = '17'

ext {
    commit = commit()
    branchName = branchName()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.slf4j:slf4j-api:2.0.13'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

String commit() {
    return 'git rev-parse HEAD'.execute().text.trim()
}

String branchName() {
    return hasProperty('branch.name') ? property('branch.name') : 'git branch --show-current'.execute().text.trim()
}

task generateHelmChart(type: Copy) {
    inputs.dir('helm/demo')
    inputs.file('build.gradle')
    from('helm') {
        include 'demo/**'
        filter(ReplaceTokens, tokens: [appVersion: version])
    }
    into('build')

}

bootJar {

    archiveFileName = 'application.jar'

    manifest{
        attributes(
                'Built-by'          : System.properties['user.name'],
                'Build-Timestamp'   : java.time.Instant.now(),
                'Build-Revision'    : commit,
                'Build-Branch'      : branchName,
                'Created-by'        : "Gradle ${gradle.gradleVersion}",
                'Build-JDK'         : "${System.properties['java.version']} ${System.properties['java.vendor']} ${System.properties['java.vm.version']}",
                'Build-OS'          : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )

    }

}

task dockerBuild(type: Exec) {
    commandLine 'docker', 'build', '-t', "agilesolutions/k8sdemo:${version}", '.'
}

task dockerPush(type: Exec) {
    commandLine 'docker', 'push', "agilesolutions/k8sdemo:${version}"
}

task helmDeploy {
    group = "helm"
    description = "Update Helm chart with the specified image tag"

    doLast {
        def helmCommand = "helm upgrade --install demo-release build/demo " +
                "--set image.tag=${version}"

        def process = helmCommand.execute()
        process.in.eachLine { line -> println line }
        process.err.eachLine { line -> System.err.println line }
        process.waitFor()

    }
}

task release(type: GradleBuild) {
    tasks = ['clean', 'build', 'dockerBuild', 'generateHelmChart', 'helmDeploy']
}
test.dependsOn generateHelmChart
compileTestJava.dependsOn generateHelmChart
generateHelmChart.dependsOn bootJar, jar

